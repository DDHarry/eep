    Author: Bj√∂rn Gustavsson <bjorn(at)erlang(dot)org>
    Status: Draft
    Type: Standards Track
    Created: 27-Oct-2015
    Erlang-Version: R19
    Post-History: 29-Oct-2015
****
EEP 45: FUNCTION macro
----



Abstract
========

This EEP proposes a new macro called `FUNCTION` that will return the
name and arity of the current function.



Specification
=============

The new predefined macro `FUNCTION` expands to a `{Name,Arity}` tuple,
where `Name` is the name of the current function and `Arity` is the
number of arguments for the current function.  Example:

    a_function(_, _) ->
      ?FUNCTION.

After preprocessing, the example will look like:

    a_function(_, _) ->
      {a_function,2}.

The `FUNCTION` macro can be used in any form that starts with an atom
followed by a left parenthesis. Thus, the following example is
legal (albeit not very useful):

    a(?FUNCTION) -> ok.

It will be expanded to:

    a({a,1}) -> ok.

`FUNCTION` will also work in the presence of other macros in the
function head. Example:

    -define(__, _, _).
    b(?FUNCTION, ?__) ->
       ok.

This code will be expanded to:

    b({b,3}, _, _) ->
      ok.

Using `FUNCTION` in an attribute will cause a compilation error.
Example:

    -attr(?FUNCTION).


The error message will look like this:

    example.erl:4: ?FUNCTION can only be used within a function



Implementation Requirements
---------------------------

This EEP does not specify exactly how the `FUNCTION` macro should be
implemented, but it does impose some requirements on the
implementation:

* The implementation must be efficient.  In particular, there should
  not be any noticeable slowdowns for modules that don't use the
  `FUNCTION` macro.

* The expansion of `FUNCTION` must be done by the `epp` module.
  Postponing expansion of `FUNCTION` to a later compiler pass is not
  acceptable, as it could cause compatibility issues with parse
  transforms and other tools that operate on the abstract format.




Examples
--------

    -define(FUNCTION_NAME, element(1, ?FUNCTION)).
    -define(FUNCTION_ARITY, element(2, ?FUNCTION)).
    -define(FUNCTION_STRING, atom_to_list(?FUNCTION_NAME) ++ "/" ++
              integer_to_list(?FUNCTION_ARITY)).

    test() ->
      ?FUNCTION_STRING.

The `test/0` function will return `"test/0"`.  Note that the
BEAM compiler will evaluate constant expressions at compilation time;
thus, `FUNCTION_STRING` will be turned into a string literal
during compilation.

    c() ->
      F = fun() -> ?FUNCTION end,
      F().

The `c/0` function will return `{c,0}`.

Motivation
==========

Many users have asked for a `FUNCTION` macro similar to `FILE`, `LINE`,
and `MODULE`.  For example: [Why no ?FUNCTION macro] [why].

The most common use case for a `FUNCTION` macro seems to be for
logging information to a log file.  Possible workarounds include
using a parse transform, using `process_info/2`, or generating and
catching an exception.  Unless the application needs a parse
transform for some other reason, implementing a parse transform
just to catch the name of the current function is cumbersome.
The other workarounds have run-time penalties.




Rationale
=========

Why not a family of `FUNCTION` macros?
--------------------------------------

It could be argued that there should be a family of macros, for
example called `FUNCTION_NAME`, `FUNCTION_ARITY`, and
`FUNCTION_STRING`.  After all, there already exist `MODULE` and
`MODULE_STRING` macros.

At the time that `MODULE_STRING` was implemented, the BEAM
compiler did not optimize complex constant expressions as well
as it does now.  Therefore, `MODULE_STRING` was added as an
optimization.

Since `FUNCTION_STRING` and the other macros can easily be defined in
terms of `FUNCTION` and since the BEAM compiler can optimize the
resulting constant expression, it is unnecessary to pollute the macro
name space with more predefined macros than strictly needed.


Why is FUNCTION allowed in a function head?
-------------------------------------------

I cannot see any practical use for the `FUNCTION` macro in a
function head.  Only allowing its use in function bodies would
make sense.  But consider this example:

    f(a) ->
      ok;
    f(?FUNCTION) ->
      ok.

To be able to reject invocations of `FUNCTION` in clauses other than
the first, the preprocessor must basically be able to parse arbitrary
Erlang code.  The only practical solution would be to use the existing
parser in the `erl_parse` module.  That would slow down the
preprocessor without providing any additional benefit.




Backwards Compatibility
=======================

Modules that define the `FUNCTION` macro will fail to compile
with a message similar to this:

    example.erl:4: redefining predefined macro 'FUNCTION'

Similarly, attempting to define `FUNCTION` from the command
line using `-D` will also fail.



Implementation
==============

The reference implementation has essentially zero extra cost for
functions that do not use the `FUNCTION` macro.  The preprocessor will
only start scanning to determine the name and arity of the current
function when an invocation of `FUNCTION` has been seen.  The scanning
will stop as soon as the right parenthesis at the end of the argument
list is found.  The name and arity will be saved and reused if another
`FUNCTION` invocation is seen in the same function definition.

The reference implementation can be fetched from Github like this:

    git fetch git://github.com/bjorng/otp.git bjorn/function-macro



[why]: http://erlang.org/pipermail/erlang-questions/2007-September/029387.html "Why no ?FUNCTION macro"

Copyright
=========

This document has been placed in the public domain.



[EmacsVar]: <> "Local Variables:"
[EmacsVar]: <> "mode: indented-text"
[EmacsVar]: <> "indent-tabs-mode: nil"
[EmacsVar]: <> "sentence-end-double-space: t"
[EmacsVar]: <> "fill-column: 70"
[EmacsVar]: <> "coding: utf-8"
[EmacsVar]: <> "End:"
[VimVar]: <> " vim: set fileencoding=utf-8 expandtab shiftwidth=4 softtabstop=4: "
